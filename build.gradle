buildscript {
	repositories {
		mavenCentral();
		jcenter();
		maven {
			name = "sonatype";
			url = "https://oss.sonatype.org/content/repositories/snapshots/";
		}
		maven {
			url "https://plugins.gradle.org/m2/";
		}
	}
	dependencies {
		classpath "gradle.plugin.net.minecrell:licenser:0.3";
	}
}

apply plugin: "java";
apply plugin: "net.minecrell.licenser";
apply plugin: "eclipse";
apply plugin: "maven-publish";
apply plugin: "signing";

group = "blue.endless";
archivesBaseName = "Jankson";
version = "2.0.0";

sourceCompatibility = 1.8;
targetCompatibility = 1.8;

repositories {
	jcenter()
	mavenCentral()
}

jar {
	manifest {
		attributes(
			"Automatic-Module-Name"       : "jankson"
		);
	}
}

task sourcesJar(type: Jar, dependsOn: classes) {
	classifier = "sources"
	from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
	classifier = "javadoc"
	from javadoc.destinationDir
}

def versionSuffix = ''
if (System.env.BUILD_NUMBER) {
	versionSuffix = '-'+System.env.BUILD_NUMBER;
}

tasks.withType(JavaCompile) {
	options.encoding = "UTF-8"
}

publishing {
	publications {
		mavenPrimary(MavenPublication) {
			from components.java;

			groupId project.group;
			artifactId "jankson";
			version project.version+versionSuffix;
			artifact sourcesJar;
			artifact javadocJar;
			
			pom {
				name = "Jankson";
				description = "JSON5 / HJSON parser and preprocessor which preserves ordering and comments";
				url = "https://github.com/falkreon/Jankson";
				
				developers {
					developer {
						id = "falkreon"
						name = "Isaac Ellingson"
						email = "falkreon@gmail.com"
					}
				}
				
				licenses {
					license {
						name = "MIT License"
						url = "https://opensource.org/licenses/MIT"
						distribution = "repo"
					}
				}
				
				scm {
					url = "https://github.com/falkreon/Jankson"
					connection = "scm:git:git@github.com:falkreon/Jankson.git"
					developerConnection = "scm:git:git@github.com:falkreon/Jankson.git"
				}
			}
		}
	}
	
	repositories {
		if (project.hasProperty("ossrhUsername")) {
			maven {
				def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
				def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots/"
				url = releasesRepoUrl; //version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
				
				credentials {
					username = ossrhUsername
					password = ossrhPassword
				}
				
			}
		}
	}
}

if (file("private.gradle").exists()) {
	apply from: "private.gradle";
}

defaultTasks "clean", "build", "sourcesJar";




dependencies {
	testCompile "junit:junit:4.12";
	
	compileOnly group: "com.google.code.findbugs", name: "jsr305", version: "3.0.2";
}

signing {
	if (project.hasProperty("signing.keyId")) {
		println("Key credentials found. Enabling jar signing.");
		sign configurations.archives;
		sign publishing.publications.mavenPrimary
	} else {
		println("Skipping jar signing");
	}
}